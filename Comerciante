Algoritmo "Comerciante"


Var

   N, i, abaixo10, entre10e20, acima20 : inteiro
   precoVenda, precoCompra, porcentagem : vetor [0..9] de real
   nome : vetor [0..9] de caractere
   totalCompra, totalVenda, lucro : real

Inicio

  escreva("Serao digitados dados de quantos produtos? ")
  leia(N)

  para i de 0 ate N-1 faca
     escreval("Produto ", i+1, ": ")
     escreva("Nome: ")
     leia(nome[i])
     escreva("Preço compra: ")
     leia(precoCompra[i])
     escreva("Preço venda: ")
     leia(precoVenda[i])
  fimpara

  para i de 0 ate N-1 faca
     porcentagem[i] <- (precoVenda[i] - precoCompra[i]) * 100 / precoCompra[i]
  fimpara
  
  escreval
  escreval("RELATORIO")
  
  abaixo10 <- 0
  entre10e20 <- 0
  acima20 <- 0
  para i de 0 ate N-1 faca
     se porcentagem[i] < 10 entao
        abaixo10 <- abaixo10 + 1
     senao
        se (porcentagem[i] >= 10) e (porcentagem[i] <= 20) entao
           entre10e20 <- entre10e20 + 1
        senao
           acima20 <- acima20 + 1
        fimse
     fimse
  fimpara
  
  escreval("Lucro abaixo de 10%: ", abaixo10)
  escreval("Lucro entre 10% e 20%: ", entre10e20)
  escreval("Lucro acima de 20%: ", acima20)
  
  totalCompra <- 0
  para i de 0 ate N-1 faca
     totalCompra <- totalCompra + precoCompra[i]
  fimpara
  
  totalVenda <- 0
  para i de 0 ate N-1 faca
     totalVenda <- totalVenda + precoVenda[i]
  fimpara
  
  lucro <- totalVenda - totalCompra
  
  escreval("Valor total de compra: ", totalCompra:1:2)
  escreval("Valor total de venda: ", totalVenda:1:2)
  escreval("Lucro total: ", lucro:1:2)
  
  
  
  

Fimalgoritmo
